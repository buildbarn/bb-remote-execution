load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@com_github_buildbarn_bb_storage//tools:container.bzl", "container_push_official", "multiarch_go_image")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

go_library(
    name = "bb_runner_lib",
    srcs = ["main.go"],
    importpath = "github.com/buildbarn/bb-remote-execution/cmd/bb_runner",
    visibility = ["//visibility:private"],
    deps = [
        "//pkg/cleaner",
        "//pkg/credentials",
        "//pkg/filesystem",
        "//pkg/proto/configuration/bb_runner",
        "//pkg/proto/runner",
        "//pkg/proto/tmp_installer",
        "//pkg/runner",
        "@com_github_buildbarn_bb_storage//pkg/filesystem",
        "@com_github_buildbarn_bb_storage//pkg/filesystem/path",
        "@com_github_buildbarn_bb_storage//pkg/global",
        "@com_github_buildbarn_bb_storage//pkg/grpc",
        "@com_github_buildbarn_bb_storage//pkg/program",
        "@com_github_buildbarn_bb_storage//pkg/util",
        "@org_golang_google_grpc//:grpc",
        "@org_golang_google_grpc//codes",
        "@org_golang_google_grpc//status",
    ],
)

go_binary(
    name = "bb_runner",
    embed = [":bb_runner_lib"],
    pure = "on",
    visibility = ["//visibility:public"],
)

multiarch_go_image(
    name = "bb_runner_bare_container",
    binary = ":bb_runner",
)

container_push_official(
    name = "bb_runner_bare_container_push",
    component = "bb-runner-bare",
    image = ":bb_runner_bare_container",
)

pkg_tar(
    name = "bb_runner_layer",
    srcs = [":bb_runner"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tini_layer",
    srcs = select({
        "@rules_go//go/platform:amd64": [
            "@com_github_krallin_tini_tini_static_amd64//file",
        ],
        "@rules_go//go/platform:arm64": [
            "@com_github_krallin_tini_tini_static_arm64//file",
        ],
    }),
    tags = ["manual"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "install_layer",
    srcs = ["install"],
    visibility = ["//visibility:public"],
)

oci_image(
    name = "bb_runner_installer_image",
    base = "@busybox",
    cmd = ["/bb"],
    entrypoint = ["/install"],
    tags = ["manual"],
    tars = [
        ":install_layer",
        ":bb_runner_layer",
        ":tini_layer",
    ],
)

[
    platform_transition_filegroup(
        name = "bb_runner_installer_image_" + arch,
        srcs = ["bb_runner_installer_image"],
        target_platform = "@rules_go//go/toolchain:linux_" + arch,
    )
    for arch in [
        "amd64",
        "arm64",
    ]
]

oci_image_index(
    name = "bb_runner_installer_container",
    images = [
        "bb_runner_installer_image_" + arch
        for arch in [
            "amd64",
            "arm64",
        ]
    ],
)

container_push_official(
    name = "bb_runner_installer_container_push",
    component = "bb-runner-installer",
    image = ":bb_runner_installer_container",
)
